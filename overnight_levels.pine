// This script marks the overnight high and low for a session.
// Ideally, the levels are found between the market open (17:00) and 04:45, central times.
//@version=5
indicator("OVN High & Low", shorttitle="OVN", overlay=true)

// ******************************************************************************************************************************
// inputs
// ******************************************************************************************************************************
session  = input.session("1700-0445", title="Trading Session", tooltip='CST Range is 1700-0445')


// ******************************************************************************************************************************
// core code
// ******************************************************************************************************************************

// SessionHigh() returns the highest price during the specified 
// session, optionally corrected for the given time zone.
// Returns 'na' when the session hasn't started or isn't on the chart.
SessionHigh(sessionTime, sessionTimeZone=syminfo.timezone) =>
    insideSession = not na(time(timeframe.period, sessionTime, sessionTimeZone))
    var float sessionHighPrice = na

    if insideSession and not insideSession[1]
        sessionHighPrice := high
    else if insideSession
        sessionHighPrice := math.max(sessionHighPrice, high)
    
    sessionHighPrice

SessionLow(sessionTime, sessionTimeZone=syminfo.timezone) =>
    insideSession = not na(time(timeframe.period, sessionTime, sessionTimeZone))
    var float sessionLowPrice = na

    if insideSession and not insideSession[1]
        sessionLowPrice := low
    else if insideSession
        sessionLowPrice := math.min(sessionLowPrice, low)
    
    sessionLowPrice

// InSession() returns 'true' when the current bar happens inside
// the specified session, corrected for the given time zone (optional).
// Returns 'false' when the bar doesn't happen in that time period,
// or when the chart's time frame is 1 day or higher. 
InSession(sessionTimes, sessionTimeZone=syminfo.timezone) =>
    not na(time(timeframe.period, sessionTimes, sessionTimeZone))


// Get the session high
sessHigh = SessionHigh(session)
sessLow = SessionLow(session)

// Show the session high on the chart
plot(sessHigh, color=color.blue, title="High")
plot(sessLow, color=color.blue, title="Low")
